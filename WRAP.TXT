       DEF  WRAP
*
       REF  LINLST,FMTLST,MGNLST
       REF  BUFALC,BUFCPY,BUFREE
       REF  ARYALC,ARYADD,ARYADR
* Workspace
       REF  WRAPSP
* Constants
       REF  SPACE,DASH

* Maximum width of a 4 inch line
LNWDTH DATA 120*4
* Character Width in n/120-inch
* 10 cpi
CPI10  DATA 12

*
* Wrap
*
* Input:
* R0 - Line index. Wrap from here.
* Output:
* R0 - >FFFF implies an error.
*
WRAP   DATA WRAPSP,WRAP+4
*
* Let R2 hold character length of each
*    line.
* Let R3 hold address of beginning of a
*    line within the paragraph.
* Let R4 hold address of beginning of
*    the following line
* Let R5 hold begining of paragraph text
* Let R6 hold address of new wrap list.
* Let R7 hold the address past the end
*    of the paragraph.
*
* characters per line
       CLR  R2
       MOV  @LNWDTH,R3
       DIV  @CPI10,R2
* address of paragraph text
       MOV  @LINLST,R0
       MOV  *R13,R1
       BLWP @ARYADR
       MOV  R1,R5
       MOV  *R5,R5
       MOV  *R5,R7               Put character length in R7
       C    *R5+,*R5+            Skip paragraph header
* address of begginng of line
       MOV  R5,R3
* address after end of paragraph
       A    R5,R7
* Create new wrap list and save address
       LI   R0,1
       BLWP @ARYALC
       CI   R0,>FFFF
       JEQ  WRPERR
       MOV  R0,R6


*
* Populate New Wrap List
*
       MOV  R3,R4
WRP0   A    R2,R4
* Is R4 past end of paragraph?
       C    R4,R7
       JHE  WRP5
*
       CB   *R4,@SPACE
       JEQ  WRP2
* Find last invisible character in early
* line
WRP1   DEC  R4
       CB   *R4,@SPACE
       JEQ  WRP3
       CB   *R4,@DASH
       JEQ  WRP3
       JMP  WRP1
* Find first visible character in next
* line
WRP2   INC  R4
       C    R4,R7
       JHE  WRP5
       CB   *R4,@SPACE
       JEQ  WRP2
       JMP  WRP4
WRP3   INC  R4
* Put address at end of wrap list
WRP4   MOV  R6,R0
       BLWP @ARYADD
       CI   R0,>FFFF
       JEQ  WRPERR
       MOV  R4,*R1
* Convert from address to string index
       S    R5,*R1
* Do next line in paragraph
       MOV  R4,R3
       JMP  WRP0
* 
* All remaining characters fit on one
* line.
WRP5


* Deallocate old wrap list.
       MOV  R5,R0
       AI   R0,-4
       BLWP @BUFREE
* Register new wrap list with paragraph
       MOV  R5,R0
       AI   R0,-2
       MOV  R6,*R0
       RTWP

WRPERR MOV  R0,*R13
       RTWP

       END