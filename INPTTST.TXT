       DEF  RUNTST,HITADR,HITLST
*
       REF  INPUT

       REF  LINLST,FMTLST,MGNLST
       REF  MAKETX,PRINTL,OPENF,CLOSEF
       REF  ARYALC,ARYADD,ARYINS,ARYDEL
       REF  ARYADR
       REF  BUFALC,BUFINT,BUFCPY

       REF  KEYSTB,KEYSTE,KEYSTX
       REF  PARINX,LININX,CHRPAX,CHRLIX
       REF  WINOFF
       REF  INSTMD
       REF  USRISR,SIX


RUNTST BLWP @OPENF
* Write notification on screen.
       BL   @WRTST
* Run each test
       BL   @TSTINT
       BL   @TST1
* Write notification on screen.
       BL   @WREND
       BLWP @CLOSEF
JMP    LIMI 2
       LIMI 0
       JMP  JMP

* Write notification
WRTST  LI   R0,STARTM
       LI   R1,ENDM-STARTM
       BLWP @PRINTL
       RT
 
* Finished testing
WREND  LI   R0,ENDM
       LI   R1,4
       BLWP @PRINTL
       RT

STARTM TEXT 'Testing'
ENDM   TEXT 'Done'
ENDME  EVEN

****************************************
*
* Initialization for individual test.
*
****************************************

TSTINT
* Initialize buffer.
       LI   R0,SPACE
       LI   R1,>2000
       BLWP @BUFINT
* Reserve space for margin and format
* and paragraph list.
       LI   R0,3
       BLWP @ARYALC
       MOV  R0,@FMTLST
       LI   R0,3
       BLWP @ARYALC
       MOV  R0,@MGNLST
       LI   R0,1
       BLWP @ARYALC
       MOV  R0,@LINLST
*
       LI   R10,INTADR
* Copy a paragraph into buffer
TSTIN1
       MOV  *R10+,R0
       MOV  R0,R2
       BLWP @BUFALC
       MOV  R0,R1
       MOV  *R10+,R0
       BLWP @BUFCPY
       MOV  R1,R4
* and a wrap list
       MOV  *R10+,R0
       MOV  R0,R2
       BLWP @BUFALC
       MOV  R0,R1
       MOV  *R10+,R0
       BLWP @BUFCPY
       MOV  R1,R5
* Put the paragraph into the
* paragraph list
       MOV  @LINLST,R0
       BLWP @ARYADD
       MOV  R0,@LINLST
       MOV  R4,*R1
* Put the wrap list in the paragraph
* header
       INCT R4
       MOV  R5,*R4
* Loop
       CI   R10,INTADE
       JL   TSTIN1
       RT

* paragraph size, paragraph address,
* wrap list size, wrap list address
INTADR DATA PAR0A-PAR0,PAR0,PAR0-WRAP0,WRAP0
       DATA PAR1A-PAR1,PAR1,PAR1-WRAP1,WRAP1
       DATA PAR2A-PAR2,PAR2,PAR2-WRAP2,WRAP2
INTADE

****************************************
*
* Starting Data
*
****************************************

*Number of tests to run
TSTCNT DATA 0

PARL0  DATA 3,1
       DATA PAR0
       DATA PAR1
       DATA PAR2

WRAP0  DATA 0,1
PAR0   DATA PAR0A-PAR0-4,WRAP0
       TEXT 'History'
PAR0A
       EVEN
WRAP1  DATA 0,1
PAR1   DATA PAR1A-PAR1-4,WRAP1
       TEXT 'Antiquity'
PAR1A
       EVEN
WRAP2  DATA 0,1
PAR2   DATA PAR2A-PAR2-4,WRAP2
       TEXT 'With a history, Wuhan is one of the most '
       TEXT 'ancient and civilized metropolitan cities '
       TEXT 'in China. Panlongcheng is located in '
       TEXT 'modern-day '
       TEXT 'Huangpi District. During the Western Zhou, '
       TEXT 'the E state controlled the present-day '
       TEXT 'Wuchang area south of the Yangtze River. '
       TEXT 'After the conquest of the E state, the '
       TEXT 'present-day Wuhan area was controlled by the '
       TEXT 'Chu state for the rest of the Western Zhou '
       TEXT 'and Eastern Zhou periods.'
PAR2A
*3,500-year-long 
*, an archaeological site associated with the Erligang culture,

LKYSTB TEXT 'KEYSTB'
LKYSTE TEXT 'KEYSTB'
LKYSTX TEXT 'KEYSTX'
LPRINX TEXT 'PARINX'
LLNINX TEXT 'LININX'
LCRPAX TEXT 'CHRPAX'
LCRLIX TEXT 'CHRLIX'
LWNOFF TEXT 'WINOFF'
LISTMD TEXT 'INSTMD'

****************************************
*
* Each Test
*
****************************************

* Test 1
* ------
* User inserted some text such that
* "With a history"
* became
* "With a 3,500-year-long history"
TST1   MOV  R11,R12
* Set position values
       CLR  R0
       MOVB R0,@INSTMD
       LI   R0,2
       MOV  R0,@PARINX
       CLR  @LININX
       LI   R0,7
       MOV  R0,@CHRPAX
       MOV  R0,@CHRLIX
       CLR  @WINOFF
* Allocate space for key stream
       LI   R0,32
       BLWP @BUFALC
       MOV  R0,@KEYSTB
       MOV  R0,@KEYSTE
       AI   R0,32
       MOV  R0,@KEYSTX
* Set up interupt routine.
       LI   R0,KEYINT
       MOV  R0,@USRISR
       LI   R0,KEYL1
       MOV  R0,@KEYSRC
* Run routine
       BLWP @INPUT
* Get address of paragraph now
       MOV  @LINLST,R0
       LI   R1,2
       BLWP @ARYADR
       MOV  *R1,R1
* Test paragraph contents
       AI   R1,4
       LI   R2,EXP1A+4
TST1A  CB   *R1+,*R2+
       JNE  ERR1A
       CI   R2,EXP1B
       JL   TST1A
* Test position values.
* Only CHRPAX and CHRLIX should have
* changed due to cursor moving.
       MOV  @PARINX,R0
       LI   R1,2
       LI   R2,LPRINX
       LI   R3,1
       BL   @COMPVL
*
       MOV  @LININX,R0
       CLR  R1
       LI   R2,LLNINX
       LI   R3,1
       BL   @COMPVL
*
       MOV  @CHRPAX,R0
       LI   R1,7+16
       LI   R2,LCRPAX
       LI   R3,1
       BL   @COMPVL
*
       MOV  @CHRLIX,R0
       LI   R1,7+16
       LI   R2,LCRLIX
       LI   R3,1
       BL   @COMPVL
*
       MOV  @WINOFF,R0
       CLR  R1
       LI   R2,LWNOFF
       LI   R3,1
       BLWP @COMPVL
*
       B    *R12

*
ERR1A  LI   R3,1
       B    @STRERR

* Pretend that "_" means there was no
* input from the keyboard.
KEYL1  TEXT '________'
       TEXT '________'
       TEXT '3_______'
       TEXT ',_______'
       TEXT '________'
       TEXT '________'
       TEXT '________'
       TEXT '500-ye__'
       TEXT '________'
       TEXT 'ar-lo___'
       TEXT 'n_______'
       TEXT '________'
       TEXT 'g ______'
       BYTE >FF
       BSS  7
KEYL1E

EXP1A DATA 0,0
      TEXT 'With a 3,500-year-long history, Wuhan is '
      TEXT 'one of '
      TEXT 'the most '
      TEXT 'ancient and civilized metropolitan cities '
      TEXT 'in China. Panlongcheng is located in '
      TEXT 'modern-day '
      TEXT 'Huangpi District. During the Western Zhou, '
      TEXT 'the E state controlled the present-day '
      TEXT 'Wuchang area south of the Yangtze River. '
      TEXT 'After the conquest of the E state, the '
      TEXT 'present-day Wuhan area was controlled by the '
      TEXT 'Chu state for the rest of the Western Zhou '
      TEXT 'and Eastern Zhou periods.'
EXP1B

****************************************

* Interrupt routine to simulate key
* presses.
KEYINT
       MOV  @KEYSRC,R0
       MOV  @KEYSTE,R1
       MOV  R0,R2
       AI   R2,8
KEYIN1 CB   *R0,@UNDRLN
       JEQ  KEYINR
       MOVB *R0+,*R1+
       C    R1,@KEYSTX
       JEQ  KEYINR
       C    R0,R2
       JL   KEYIN1
KEYINR MOV  R2,@KEYSRC
       MOV  R1,@KEYSTE
       RT
* Cancel the old interupt.
KEYCNL LI   R0,NOPINT
       MOV  R0,@USRISR
       JMP  KEYINR
* An interupt that does nothing.
NOPINT RT
* Contains the address at which the next
* simulated keystroke is found.
* Every time the interupt routine is hit
* take up to eight characters from the
* position starting at this address.
* At the end of the routine, the value
* of KEYSRC should be 8 bytes higher
* than when the routine started.
* The 8-byte block that starts >FF
* signals the end of simulated input,
* but let the INPUT routine worry about
* that.
KEYSRC DATA 0
HITADR DATA HITLST
UNDRLN TEXT '_'
       TEXT 'HIT LIST'
HITLST BSS  >2000
       EVEN

* Compare contents of R0 and R1.
* Report if they don't match.
*
* R0 - Actual value
* R1 - Expected value
* R2 - Address of six byte string
*      containing variable name
* R3 - text number
COMPVL
       C    R0,R1
       JEQ  COMPRT
*
       LI   R1,COMPM2+13
       BLWP @MAKETX
*
       MOV  R3,R0
       LI   R1,COMPMS+5
       BLWP @MAKETX
*
       LI   R1,COMPM1+9
       MOVB *R2+,*R1+
       MOVB *R2+,*R1+
       MOVB *R2+,*R1+
       MOVB *R2+,*R1+
       MOVB *R2+,*R1+
       MOVB *R2+,*R1+
*
       LI   R0,COMPMS
       LI   R1,COMPM3-COMPMS
       BLWP @PRINTL
*
COMPRT B    *R12
*
COMPMS TEXT 'Test .... failed. '
COMPM1 TEXT 'Value in ...... is wrong. '
COMPM2 TEXT 'Actual value .....'
COMPM3 EVEN

STRERR MOV  R3,R0
       LI   R1,STRMS+5
       BLWP @MAKETX
*
       LI   R0,STRMS
       LI   R1,STRMS1-STRMS
       BLWP @PRINTL
*
       B    *R12
STRMS  TEXT 'Test .... failed. '
       TEXT 'Strings do not match.'
STRMS1 EVEN

****************************************

SPACE  BSS  >2000
SPCEND

       END