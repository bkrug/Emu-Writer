100 PRINT "SOURCE DIS/VAR 80 FILE:"
110 INPUT SOURCE$
120 PRINT "DESTINATION EMUWRITER FILE:"
130 INPUT DEST$
140 OPEN #1:SOURCE$,DISPLAY ,VARIABLE 80,INPUT
150 OPEN #2:DEST$,DISPLAY ,FIXED 64,OUTPUT
160 OUTPUTLINE$="DocOfEmuWriter"&CHR$(1)&CHR$(2)
170 LINPUT #1:INPUTLINE$
180 IF LEN(OUTPUTLINE$)=0 THEN 210
190 LASTCHR$=SEG$(OUTPUTLINE$,LEN(OUTPUTLINE$),1)
200 IF LASTCHR$=CHR$(13)OR LASTCHR$=" " THEN 220
210 OUTPUTLINE$=OUTPUTLINE$&" "
220 FOR I=1 TO LEN(INPUTLINE$)
230 ONECHR$=SEG$(INPUTLINE$,I,1)
240 IF ASC(ONECHR$)=13 THEN 270
250 IF ASC(ONECHR$)<32 THEN 280
260 IF ASC(ONECHR$)>126 THEN 280
270 OUTPUTLINE$=OUTPUTLINE$&ONECHR$
280 NEXT I
290 GOSUB 390
300 IF EOF(1)=0 THEN 170
310 CLOSE #1
320 OUTPUTLINE$=OUTPUTLINE$&CHR$(3)&CHR$(80)&CHR$(66)&CHR$(0)&CHR$(0)&CHR$(0)&CHR$(3)
330 IF LEN(OUTPUTLINE$)<=64 THEN 360
340 GOSUB 390
350 IF LEN(OUTPUTLINE$)=0 THEN 370
360 PRINT #2:OUTPUTLINE$
370 CLOSE #2
380 END
390 REM  **OUTPUT FIXED 64 RECORD** 
400 IF LEN(OUTPUTLINE$)<64 THEN 460
410 REC$=SEG$(OUTPUTLINE$,1,64)
420 PRINT #2:REC$
430 PRINT REC$
440 REMAINING=LEN(OUTPUTLINE$)-64
450 OUTPUTLINE$=SEG$(OUTPUTLINE$,65,REMAINING)
460 RETURN