       DEF  RUNTST
	   DEF  KEYDVC,KEYPRS,USRISR
	   DEF  NOKEY
	   DEF  KEYINT
*
       REF  VSBW,KSCAN,VMBW
	   REF  TIMER,PREVKY
	   REF  BUFSTR,BUFEND,BUFWRT,BUFRD

* Keyboard device to be checked
KEYDVC EQU  >8374
* Key pressed
KEYPRS EQU  >8375
* Key status
KEYSTS EQU  >837C
* Address defining address of user-
* defined service routine
USRISR EQU  >83C4

* The delay before initial repeat
WAIT1  DATA >18
* The delay before additional repeats
WAIT2  DATA >4

NOKEY  BYTE >FF

* Use an interupt to record key presses
* so that if the computer is working on
* a long process the keys will still be
* recorded.
KEYINT BLWP @KSCAN
       CB   @KEYPRS,@NOKEY
       JNE  KEYDWN
       MOVB @KEYPRS,@PREVKY
       RT
* A key has been pressed.
KEYDWN CB   @KEYPRS,@PREVKY
       JNE  KEYNEW
       DEC  @TIMER
       JH   KEYEND
* The Key is being repeated
KEYAGN MOV  @WAIT2,@TIMER
       JMP  KEYCPY
* The Key is new
KEYNEW MOV  @WAIT1,@TIMER
       MOVB @KEYPRS,@PREVKY
* Copy the key to the key buffer
* Auto increment the buffer position
KEYCPY MOV  @BUFWRT,R0
       MOVB @KEYPRS,*R0+
* If the next position is past the
* buffer end, move to the buffer start
	   CI   R0,BUFEND
	   JL   KEY1
	   LI   R0,BUFSTR
* Update BUFWRT with the new buffer
* position, unless this would cause
* BUFWRT to equal BUFRD
KEY1   C    R0,@BUFRD
	   JEQ  KEYEND
       MOV  R0,@BUFWRT
*
KEYEND RT
       END