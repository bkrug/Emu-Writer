       DEF  TSTLST,RSLTFL
       REF  AEQ,ANEQ,ABLCK
*
       REF  WRAP,LINLST,FMTLST,MGNLST
       REF  BUFINT,BUFALC,BUFCPY
       

TSTLST DATA TSTEND-TSTLST-2/8
       DATA WRP1
       TEXT 'WRP1  '
       DATA WRP2
       TEXT 'WRP2  '
       DATA WRP3
       TEXT 'WRP3  '
       DATA WRP4
       TEXT 'WRP4  '
       DATA WRP5
       TEXT 'WRP5  '
TSTEND
RSLTFL BYTE RSLTFE-RSLTFL-1
       TEXT 'DSK2.TESTRESULT.TXT'
RSLTFE
       EVEN

* PARAGRAPH FORMAT
*
* Data Format:
* Byte 0&1: Length of string (paragraph)
* Byte 2&3: Address of Wrap List
* Byte >=4: The string
*
* At 10 CPI bytes 1&2 will normally be
* equal to either (byte0 - 1)*12 or
* (byte0 - 2)*12. Each character is
* 12/120 inch wide. And spaces or line
* endings are not included in text
* length.
*

MNERR  TEXT 'No error should have been '
       TEXT 'detected.'
MNERRE
MWRP   TEXT 'Wrap lists do not match '
MWRPE  EVEN
FAKEAD DATA >FFFE

*
* The entire paragraph will be
* re-wrapped. A word will be brought
* down to the next line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
*
WRP1
* Arrange
       LI   R12,STACK
       DECT R12
       MOV  R11,*R12
* The wrap list must go in the block
* of memory allocated to the chunck
* manager because it may be deallocated.
       LI   R2,WRPL1
       LI   R3,WRPE1-WRPL1
       LI   R4,PAR1A
       BL   @CPYWRP
*
       LI   R0,PARL1
       MOV  R0,@LINLST
       LI   R0,FMT1
       MOV  R0,@FMTLST
       LI   R0,MRGN1
       MOV  R0,@MGNLST
* Act
       LI   R0,0
       BLWP @WRAP
* Assert
*  No error should be detected
       MOV  R0,R1
       SETO R0
       LI   R2,MNERR
       LI   R3,MNERRE-MNERR
       BLWP @ANEQ
*  Wrap list should match expected
       LI   R0,WRPE1
       MOV  @PAR1A+2,R1
       LI   R2,WRPE1E-WRPE1
       LI   R3,MWRP
       LI   R4,MWRPE-MWRP
       BLWP @ABLCK
*
       MOV  *R12+,R11
       RT


* Paragraph List
PARL1  DATA 5,1
       DATA PAR1A
       DATA FAKEAD
       DATA FAKEAD
       DATA FAKEAD
       DATA FAKEAD

PAR1A  DATA 47+41+6
       DATA WRPL1
       TEXT 'Wuhan is the capital of Hubei province, '
       TEXT 'China, and is the most populous city in '
       TEXT 'Central China.'
       EVEN
WRPL1  DATA 2,1,47,47+41
WRPE1  DATA 2,1,40,40+40
WRPE1E

FMT1   DATA 0,3

MRGN1  DATA 1,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* down to the next line, and a new line
* will be created.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
*
WRP2
* Arrange
       LI   R12,STACK
       DECT R12
       MOV  R11,*R12
* The wrap list must go in the block
* of memory allocated to the chunck
* manager because it may be deallocated.
       LI   R2,WRPL2
       LI   R3,WRPE2-WRPL2
       LI   R4,PAR2A
       BL   @CPYWRP
*
       LI   R0,PARL2
       MOV  R0,@LINLST
       LI   R0,FMT2
       MOV  R0,@FMTLST
       LI   R0,MRGN2
       MOV  R0,@MGNLST
* Act
       LI   R0,1
       BLWP @WRAP
* Assert
*  No error should be detected
       MOV  R0,R1
       SETO R0
       LI   R2,MNERR
       LI   R3,MNERRE-MNERR
       BLWP @ANEQ
*  Wrap list should match expected
       LI   R0,WRPE2
       MOV  @PAR2A+2,R1
       LI   R2,WRPE2E-WRPE2
       LI   R3,MWRP
       LI   R4,MWRPE-MWRP
       BLWP @ABLCK
*
       MOV  *R12+,R11
       RT

* Paragraph List
PARL2  DATA 3,1
       DATA FAKEAD
       DATA PAR2A
       DATA FAKEAD

PAR2A  DATA 60+41+30
       DATA WRPL2
       TEXT 'It lies in the eastern Jianghan Plain on '
       TEXT 'the middle reaches of the Yangtze River '
       TEXT 'at the intersection of the Yangtze and '
       TEXT 'Han rivers.'
       EVEN
WRPL2  DATA 2,1,60,60+41
WRPE2  DATA 3,1,41,41+40,41+40+39
WRPE2E

FMT2   DATA 0,3

MRGN2  DATA 1,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414
	   
*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* up to the first line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
*
WRP3
* Arrange
       LI   R12,STACK
       DECT R12
       MOV  R11,*R12
* The wrap list must go in the block
* of memory allocated to the chunck
* manager because it may be deallocated.
       LI   R2,WRPL3
       LI   R3,WRPE3-WRPL3
       LI   R4,PAR3A
       BL   @CPYWRP
*
       LI   R0,PARL3
       MOV  R0,@LINLST
       LI   R0,FMT3
       MOV  R0,@FMTLST
       LI   R0,MRGN3
       MOV  R0,@MGNLST
* Act
       LI   R0,2
       BLWP @WRAP
* Assert
*  No error should be detected
       MOV  R0,R1
       SETO R0
       LI   R2,MNERR
       LI   R3,MNERRE-MNERR
       BLWP @ANEQ
*  Wrap list should match expected
       LI   R0,WRPE3
       MOV  @PAR3A+2,R1
       LI   R2,WRPE3E-WRPE3
       LI   R3,MWRP
       LI   R4,MWRPE-MWRP
       BLWP @ABLCK
*
       MOV  *R12+,R11
       RT

PARL3  DATA 4,1
       DATA FAKEAD
       DATA FAKEAD
       DATA PAR3A
       DATA FAKEAD

PAR3A  DATA 19+32+39+32
       DATA WRPL3
       TEXT 'Arising out of the conglomeration of '
       TEXT 'three cities, Wuchang, Hankou, and '
       TEXT 'Hanyang, Wuhan is known as "China"s '
       TEXT 'Thoroughfare".'
       EVEN
WRPL3  DATA 3,1,19,19+32,19+32+39
WRPE3  DATA 3,1,37,37+35,37+35+36
WRPE3E

FMT3   DATA 0,3

MRGN3  DATA 1,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* up to the first line. The final line
* of the paragraph will be removed.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
WRP4
* Arrange
       LI   R12,STACK
       DECT R12
       MOV  R11,*R12
* The wrap list must go in the block
* of memory allocated to the chunck
* manager because it may be deallocated.
       LI   R2,WRPL4
       LI   R3,WRPE4-WRPL4
       LI   R4,PAR4A
       BL   @CPYWRP
*
       LI   R0,PARL4
       MOV  R0,@LINLST
       LI   R0,FMT4
       MOV  R0,@FMTLST
       LI   R0,MRGN4
       MOV  R0,@MGNLST
* Act
       LI   R0,3
       BLWP @WRAP
* Assert
*  No error should be detected
       MOV  R0,R1
       SETO R0
       LI   R2,MNERR
       LI   R3,MNERRE-MNERR
       BLWP @ANEQ
*  Wrap list should match expected
       LI   R0,WRPE4
       MOV  @PAR4A+2,R1
       LI   R2,WRPE4E-WRPE4
       LI   R3,MWRP
       LI   R4,MWRPE-MWRP
       BLWP @ABLCK
*
       MOV  *R12+,R11
       RT

PARL4  DATA 6,1
       DATA FAKEAD
       DATA FAKEAD
       DATA FAKEAD
       DATA PAR4A
       DATA FAKEAD
       DATA FAKEAD

PAR4A  DATA 29+36+36+35+7
       DATA WRPL4
       TEXT 'It is a major transportation '
       TEXT 'hub, with dozens of railways, roads '
       TEXT 'and expressways passing through the '
       TEXT 'city and connecting to other major '
       TEXT 'cities.'
       EVEN
WRPL4  DATA 4,1,29,29+36,29+36+36,29+36+36+35
WRPE4  DATA 3,1,39,39+30,39+30+41
WRPE4E

FMT4   DATA 1,3
* 12-Cpi at paragraph start
       DATA 4,0,0,10

MRGN4  DATA 2,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414
* 1-inch margins
       DATA 7,>0000,>1414,>1414

*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 12 CPI
*
WRP5
* Arrange
       LI   R12,STACK
       DECT R12
       MOV  R11,*R12
* The wrap list must go in the block
* of memory allocated to the chunck
* manager because it may be deallocated.
       LI   R2,WRPL5
       LI   R3,WRPE5-WRPL5
       LI   R4,PAR5A
       BL   @CPYWRP
*
       LI   R0,PARL5
       MOV  R0,@LINLST
       LI   R0,FMT5
       MOV  R0,@FMTLST
       LI   R0,MRGN5
       MOV  R0,@MGNLST
* Act
       LI   R0,4
       BLWP @WRAP
* Assert
*  No error should be detected
       MOV  R0,R1
       SETO R0
       LI   R2,MNERR
       LI   R3,MNERRE-MNERR
       BLWP @ANEQ
*  Wrap list should match expected
       LI   R0,WRPE5
       MOV  @PAR5A+2,R1
       LI   R2,WRPE5E-WRPE5
       LI   R3,MWRP
       LI   R4,MWRPE-MWRP
       BLWP @ABLCK
*
       MOV  *R12+,R11
       RT

PARL5  DATA 8,1
       DATA FAKEAD
       DATA FAKEAD
       DATA FAKEAD
       DATA FAKEAD
       DATA PAR5A
       DATA FAKEAD
       DATA FAKEAD
       DATA FAKEAD

PAR5A  DATA 36+47+45
       DATA WRPL5
       TEXT 'Because of its key role in domestic '
       TEXT 'transportation, Wuhan is sometimes referred '
       TEXT 'to '
       TEXT 'as "the Chicago of China" by foreign sources.'
       EVEN
WRPL5  DATA 4,1,36,36+44,36+44+11,36+44+11+11
WRPE5  DATA 2,1,36,36+47
WRPE5E

FMT5   DATA 1,3
* 12-Cpi at paragraph start
       DATA 4,0,0,10

MRGN5  DATA 2,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414
* 1-inch margins
       DATA 7,>0000,>1414,>1414

********


FMT0   DATA 3,3
* 12-Cpi at paragraph start
       DATA 4,0,0,10
* 5-Cpi in middle of second line of 
* paragraph
       DATA 5,64,0,24
* 12-Cpi at paragraph start
       DATA 7,0,0,10
FMT0E

MRGN0  DATA 4,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414
* 1-inch margins
       DATA 7,>0000,>1414,>1414
* 2-inch margins, 1-inch indent
       DATA 9,>0014,>2828,>1414
* 2.5-inch margins,
* 0.5-inch hanging indent
       DATA 10,>00F6,>3232,>1414
MRGN0E

*
* Copy Wrap Address to buffer.
* Register its location in
* paragraph object.
* Input:
*  R2 - Wrap list source address
*  R3 - length of wrap list
*  R4 - address of paragraph
* Output:
*  R0-R2,R4 will change
CPYWRP
       LI   R0,SPACE
       LI   R1,SPCEND-SPACE
       BLWP @BUFINT
*
       MOV  R3,R0
       BLWP @BUFALC
*
       MOV  R0,R1
       MOV  R2,R0
       MOV  R3,R2
       BLWP @BUFCPY
* Update paragraph object's wrap address
       INCT R4
       MOV  R1,*R4
*
       RT

SPACE  BSS  >400
SPCEND
STACKS BSS  >10
STACK
       END