       DEF  RUNTST
       DEF  MGNLST
*
       REF  WRAP,LINLST,FMTLST
       REF  BUFINT,BUFALC,BUFREE,BUFCPY
       REF  MAKETX,PRINTL,OPENF,CLOSEF
       REF  ARYALC,ARYADD,ARYINS,ARYDEL
       REF  VMBW

RUNTST BLWP @OPENF
* Initialize the memory buffer.
       BL   @INTBUF
* Write notification on screen.
       BL   @WRTST
* Loop through every test.
       BL   @TSTLP
* Write notification on screen.
       BL   @WREND
       BLWP @CLOSEF
JMP    LIMI 2
       LIMI 0
       JMP  JMP

* Write notification
WRTST  LI   R0,STARTM
       LI   R1,ENDM-STARTM
       BLWP @PRINTL
       RT
 
* Finished testing
WREND  LI   R0,ENDM
       LI   R1,4
       BLWP @PRINTL
       RT

STARTM TEXT 'Testing'
ENDM   TEXT 'Done'
ENDME  EVEN

*
* Initialize buffer.
*
INTBUF LI   R0,SPACE
       LI   R1,SPCEND-SPACE
       BLWP @BUFINT
       MOV  R0,R0
       JEQ  INTBF2
       LI   R0,INTMSG
       LI   R1,INTMSE-INTMSG
       BLWP @PRINTL
       B    @JMP
INTBF2 RT
INTMSG TEXT 'Failed to initialize memory buffer.'
INTMSE EVEN

****************************************
*
* Main Test Loop.
*
****************************************

TSTLP  MOV  R11,R12
       LI   R8,EXPLIN
       MOV  R8,@EXPLNR
       LI   R8,EXPFMT
       MOV  R8,@EXPFMR
       LI   R8,WRPLN
       MOV  R8,@WRPLNR
* Use R10 to track which test this is
       CLR  R10
* Loop through each test.
TSTLP1
* Initialize the Line List.
       BL   @INITLL
* Copy each line into the line list.
       BL   @CPYLIN
* Copy the format list for this test.
       BL   @INTFMT
* Setup wrap parameters.
* Place index of line to wrap in R0.
       MOV  @WRPLNR,R0
       MOV  *R0,R0
* Wrap TEXT
       BLWP @WRAP
       CI   R0,>FFFF
       JNE  TSTLP2
       B    @LPERR3
* Test Line List and Format List.
TSTLP2 BL   @TSTLIN
       BL   @TSTFMT
* One test is complete.
* Reinitialize memory buffer.
       LI   R0,SPACE
       LI   R1,SPCEND-SPACE
       BLWP @BUFINT
* Run the next test
       INCT @EXPLNR
       INCT @EXPFMR
       INCT @WRPLNR
       INC  R10
       C    R10,@TSTCNT
       JL   TSTLP1
*
       B    *R12

* Address within line list.
WRPLNR DATA 0
* Address within EXPLIN.
EXPLNR DATA 0
* Address within EXPFMT
EXPFMR DATA 0

*
* Initialize Line List for the
* current test.
* Reserve space for the list and store
* length of the list in first word.
*
* Input:
*   R10 - Index of current test
* Output:
*   R0 - address of the line list within
*        buffer
*   R8 - address of the source line list
*        (like LIN0)
INITLL
* Reserve space for an array with items
* of size 2 bytes.
       LI   R0,1
       BLWP @ARYALC
* Copy starting line list to LINLST.
* R0 holds the location that LINLST is
* stored at.
       MOV  R0,@LINLST
       RT

*
* Copy strings into buffer.
* Each string starts with the number of
* characters stored in the 1st byte.
* The memory consumed is equal to that 
* plus three.
*
* Also, store the address of copied
* strings in the line list.
*
* Input:
*   R0 - Address of Line List
* Output:
*   R8,R9,R1,R0,R2,R3
CPYLIN
* Get address of starting line list
*        (Like LINL0)
       LI   R8,STRLIN
       A    R10,R8
       A    R10,R8
       MOV  *R8,R8
* Store number of lines to copy in R3.
       MOV  *R8,R3
* R8 holds address of the address
* the lines of text
       C    *R8+,*R8+
* R0 will contain address of line to
* copy.
CPYL2
* Change R0's value to number of bytes
* to copy and allocate that much space.
       MOV  *R8,R0
       MOVB *R0,R0
       SRL  R0,8
       INC  R0
       MOV  R0,R2
       BLWP @BUFALC
* Copy the string. R2 still holds byte
* size to copy.
       MOV  R0,R1
       MOV  *R8,R0
       BLWP @BUFCPY
* Put the new string address in the
* line list.
       MOV  R1,R9
       MOV  @LINLST,R0
       BLWP @ARYADD
       MOV  R0,@LINLST
       MOV  R9,*R1
*
       INCT R8
* If there are more lines, loop.
       DEC  R3
       JH   CPYL2
*
       RT

*
* Initialize format list.
*
INTFMT
* Get address of starting format list
       LI   R8,STRFMT
       A    R10,R8
       A    R10,R8
       MOV  *R8,R8
* R8 now contains the address of 
* initial format list.
* Reserve space for format list in buffer.
       MOV  *R8,R0
       INC  R0
       SLA  R0,2
       MOV  R0,R2
       BLWP @BUFALC
* Copy starting line list to FMTLST.
* R0 holds the location that FMTLST is
* stored at.
       MOV  R0,@FMTLST
* Copy formats into buffer.
       MOV  R0,R1
       MOV  R8,R0
       BLWP @BUFCPY
*
       RT

*
* Test Line List results
*
* Assign R8 the address of the expected
* line list.
* Assign R9 the address of the actual
* line list.
TSTLIN MOV  @EXPLNR,R8
       MOV  *R8,R8
       MOV  @LINLST,R9
* Confirm that the number of lines in
* the list is correct.
       C    *R8,*R9
       JNE  LPERR1
* R5 shall store number of lines.
       MOV  *R8,R5
* Now loop through each line.
* R8 holds address of the address of the
* expected line
* R9 holds address of the address of the
* actual line
TSTLN2 INCT R8
       INCT R9
* Store position within the line
* R6 and R7 hold the position within
* each line.
       MOV  *R8,R6
       MOV  *R9,R7
* R4 shall store character length
       MOVB *R6,R4
       SRL  R4,8
* Confirm that the line character length
* is correct.
       CB   *R6+,*R7+
       JNE  LPERR2
* Confirm string contents are correct.
TSTLN3 CB   *R6+,*R7+
       JNE  LPERR4
       DEC  R4
       JH   TSTLN3
* See if we should check another line
       DEC  R5
       JH   TSTLN2
       RT

*
* Test format list
*
TSTFMT
* Confirm format contents are correct.
       MOV  @EXPFMR,R8
       MOV  *R8,R8
       MOV  @FMTLST,R9
*
       C    *R8,*R9
       JNE  LPERR5
*
       MOV  *R8,R4
       SLA  R4,2
TSTFM1 CB   *R8+,*R9+
       JNE  TSTFE1
       DEC  R4
       JH   TSTFM1
       RT
TSTFE1 B    @LPERR6

* Test result contains wrong number of
* lines.
LPERR1
       LI   R1,LPER1M+5
       BL   @WRTNAM
*
       MOV  @LINLST,R0
       MOV  *R0,R0
       LI   R1,LPER1N-4
       BLWP @MAKETX
*
       LI   R0,LPER1M
       LI   R1,LPER1N-LPER1M
       BLWP @PRINTL
       B    *R12

* Test line contains wrong number of 
* characters.
LPERR2
       LI   R1,LPER2M+5
       BL   @WRTNAM
*
       MOV  R9,R0
       S    @LINLST,R0
       SRL  R0,1
       DEC  R0
       LI   R1,LPER2N
       BLWP @MAKETX
*
       MOV  *R7,R0
       SRL  R0,8
       LI   R1,LPER2N+11
       BLWP @MAKETX
*
       LI   R0,LPER2M
       LI   R1,LPER2O-LPER2M
       BLWP @PRINTL
*
       B    *R12

LPERR3
       LI   R1,LPER3M+5
       BL   @WRTNAM
*
       LI   R0,LPER3M
       LI   R1,LPER3N-LPER3M
       BLWP @PRINTL
*
       B    *R12

*
* Test line contents don't match
LPERR4 
* Copy test name.
       LI   R1,LPER4M+5
       BL   @WRTNAM
* Record line index
       MOV  R9,R0
       S    @LINLST,R0
       SRL  R0,1
       DEC  R0
       LI   R1,LPER4N
       BLWP @MAKETX
* Copy expected text
       LI   R1,LPER4O
       MOV  *R8,R0
       MOVB *R0,R2
       INC  R0
       SRL  R2,8
       CI   R2,60
       JL   LPER4A
       LI   R2,60
LPER4A BLWP @BUFCPY
* Copy "|-- Actual:" into message
       LI   R0,LPER4Q
       A    R2,R1
       LI   R2,LPER4R-LPER4Q
       BLWP @BUFCPY
* Copy actual text
       A    R2,R1
       MOV  *R9,R0
       MOVB *R0,R2
       INC  R0
       SRL  R2,8
       CI   R2,60
       JL   LPER4B
       LI   R2,60
LPER4B BLWP @BUFCPY
* Copy "|--" into message.
       LI   R0,LPER4Q
       A    R2,R1
       LI   R2,3
       BLWP @BUFCPY
* Output error message.
       LI   R0,LPER4M
       LI   R1,LPER4Q-LPER4M
       BLWP @PRINTL
*
       B    *R12

LPERR5
       LI   R1,LPER5M+5
       BL   @WRTNAM
*
       MOV  *R9,R0
       LI   R1,LPER5N-4
       BLWP @MAKETX
*
       LI   R0,LPER5M
       LI   R1,LPER5N-LPER5M
       BLWP @PRINTL
*
       B    *R12

LPERR6
       LI   R1,LPER6M+5
       BL   @WRTNAM
*
       LI   R0,LPER6M
       LI   R1,LPER6N-LPER6M
       BLWP @PRINTL
*
       B    *R12

LPER1M TEXT 'Test ...... failed. Wrong '
       TEXT 'number of lines. We see '
       TEXT '....'
LPER1N EVEN

LPER2M TEXT 'Test ...... failed. Wrong '
       TEXT 'number of characters in '
       TEXT 'line '
LPER2N TEXT '.... found .... characters'
LPER2O EVEN

LPER3M TEXT 'Test ...... failed. WRAP '
       TEXT 'could not allocate space.'
LPER3N EVEN

LPER4M TEXT 'Test ...... failed. '
       TEXT 'Contents of line '
LPER4N TEXT '.... were wrong. Expected:'
LPER4O TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '           '
LPER4P EVEN
LPER4Q TEXT '|-- Actual:'
LPER4R EVEN

LPER5M TEXT 'Text ...... failed. Wrong '
       TEXT 'number of formats. We see '
       TEXT '....'
LPER5N EVEN

LPER6M TEXT 'Test ...... failed. Wrong '
       TEXT 'format list contents.'
LPER6N EVEN

WRTNAM
* Copy test name.
       LI   R0,TSTNAM
       MOV  R10,R3
       MPY  @SIX,R3
       A    R4,R0
       LI   R2,6
       BLWP @BUFCPY
       RT

SPACE  BSS  >2000
SPCEND

****************************************
*
* Test data
*
****************************************

*Number of tests to run
TSTCNT DATA 5
*Lines to wrap from
WRPLN  DATA 0,3,6,10,15
*Original Line Lists
STRLIN DATA LINL0,LINL0,LINL0,LINL0,LINL0
STRLNE
*Expected Line Lists
EXPLIN DATA LINE1,LINE2,LINE3,LINE4,LINE5
EXPLNE
*Original Format Lists
STRFMT DATA FMT0,FMT0,FMT0,FMT0,FMT0
STRFME
*Expected Format Lists
EXPFMT DATA FMT0,FMTE2,FMT0,FMTE4,FMT0
EXPFME
*Test names
TSTNAM TEXT 'LIN1  LIN2  LIN3  LIN4  LIN5  '
TSTNME
SIX    DATA 6

LINL0  DATA 20,1
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
       DATA LIN5A,LIN5B,LIN5C,LIN5D,LIN5E
LINL0E

* Expected Line results
LINE1  DATA 20,1
       DATA EXP1A,EXP1B,EXP1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
       DATA LIN5A,LIN5B,LIN5C,LIN5D,LIN5E
LINE1E
* Expected Line results
LINE2  DATA 21,1
       DATA LIN1A,LIN1B,LIN1C
       DATA EXP2A,EXP2B,EXP2C,EXP2D
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
       DATA LIN5A,LIN5B,LIN5C,LIN5D,LIN5E
LINE2E
* Expected Line results
LINE3  DATA 20,1
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA EXP3A,EXP3B,EXP3C,EXP3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
       DATA LIN5A,LIN5B,LIN5C,LIN5D,LIN5E
LINE3E
* Expected Line results
LINE4  DATA 19,1
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA EXP4A,EXP4B,EXP4C,EXP4D
       DATA LIN5A,LIN5B,LIN5C,LIN5D,LIN5E
LINE4E
* Expected Line results
LINE5  DATA 19,1
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
       DATA EXP5A,EXP5B,EXP5C,EXP5D
LINE5E

*
* Data Format:
* Byte 0: Number of characters in the
*         string.
* Byte 1&2: Length of text in format:
*           n/120 inches.
* Byte >=3: The string
*
* At 10 CPI bytes 1&2 will normally be
* equal to either (byte0 - 1)*12 or
* (byte0 - 2)*12. Each character is
* 12/120 inch wide. And spaces or line
* endings are not included in text
* length.

*
* The entire paragraph will be
* re-wrapped. A word will be brought
* down to the next line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN1A  BYTE 47
*       DATA 552
       TEXT 'Wuhan is the capital of Hubei province, '
       TEXT 'China, '
       EVEN
LIN1B  BYTE 41
*       DATA 480
       TEXT 'and is the most populous city in Central '
       EVEN
LIN1C  BYTE 8
*       DATA 72
       TEXT 'China.'
       BYTE 13,10
       EVEN
EXP1A  BYTE 40
*       DATA 468
       TEXT 'Wuhan is the capital of Hubei province, '
       EVEN
EXP1B  BYTE 40
*       DATA 468
       TEXT 'China, and is the most populous city in '
       EVEN
EXP1C  BYTE 16
*       DATA 168
       TEXT 'Central China.'
       BYTE 13,10
       EVEN

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* down to the next line, and a new line
* will be created.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN2A  BYTE 60
*       DATA 708
       TEXT 'It lies in the eastern Jianghan Plain '
       TEXT 'on the middle reaches '
       EVEN
LIN2B  BYTE 41
*       DATA 480
       TEXT 'of the Yangtze River at the intersection '
       EVEN
LIN2C  BYTE 32
*       DATA 504
       TEXT 'of the Yangtze and Han rivers.'
       BYTE 13,10
       EVEN
EXP2A  BYTE 41
*       DATA 444
       TEXT 'It lies in the eastern Jianghan Plain on '
       EVEN
EXP2B  BYTE 40
*       DATA 432
       TEXT 'the middle reaches of the Yangtze River '
       EVEN
EXP2C  BYTE 39
*       DATA 384
       TEXT 'at the intersection of the Yangtze and '
       EVEN
EXP2D  BYTE 13
*       DATA 300
       TEXT 'Han rivers.'
       BYTE 13,10
       EVEN

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* up to the first line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN3A  BYTE 19
*       DATA 216
       TEXT 'Arising out of the '
       EVEN
LIN3B  BYTE 32
*       DATA 372
       TEXT 'conglomeration of three cities, '
       EVEN
LIN3C  BYTE 39
*       DATA 456
       TEXT 'Wuchang, Hankou, and Hanyang, Wuhan is '
       EVEN
LIN3D  BYTE 34
*       DATA 384
       TEXT 'known as "China"s Thoroughfare".'
       BYTE 13,10
       EVEN
EXP3A  BYTE 37
*       DATA 432
       TEXT 'Arising out of the conglomeration of '
       EVEN
EXP3B  BYTE 35
*       DATA 408
       TEXT 'three cities, Wuchang, Hankou, and '
       EVEN
EXP3C  BYTE 36
*       DATA 420
       TEXT 'Hanyang, Wuhan is known as "China"s '
       EVEN
EXP3D  BYTE 16
*       DATA 168
       TEXT 'Thoroughfare".'
       BYTE 13,10
       EVEN

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* up to the first line. The final line
* of the paragraph will be removed.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN4A  BYTE 29
*       DATA 336
       TEXT 'It is a major transportation '
       EVEN
LIN4B  BYTE 36
*       DATA 420
       TEXT 'hub, with dozens of railways, roads '
       EVEN
LIN4C  BYTE 36
*       DATA 420
       TEXT 'and expressways passing through the '
       EVEN
LIN4D  BYTE 35
*       DATA 408
       TEXT 'city and connecting to other major '
       EVEN
LIN4E  BYTE 9
*       DATA 84
       TEXT 'cities.'
       BYTE 13,10
       EVEN
EXP4A  BYTE 39
*       DATA 456
       TEXT 'It is a major transportation hub, with '
       EVEN
EXP4B  BYTE 30
*       DATA 468
       TEXT 'dozens of railways, roads and '
       EVEN
EXP4C  BYTE 41
*       DATA 432
       TEXT 'expressways passing through the city and '
       EVEN
EXP4D  BYTE 35
*       DATA 444
       TEXT 'connecting to other major cities.'
       BYTE 13,10
       EVEN

*
* Doesn't test much itself, but it has
* some formatting attached to it.
*
LIN5A  BYTE 36
       TEXT 'Because of its key role in domestic '
       EVEN
LIN5B  BYTE 44
       TEXT 'transportation, Wuhan is sometimes referred '
       EVEN
LIN5C  BYTE 11
       TEXT 'to as "the '
       EVEN
LIN5D  BYTE 11
       TEXT 'Chicago of '
       EVEN
LIN5E  BYTE 28
       TEXT 'China" by foreign sources.'
       BYTE 13,10
       EVEN
EXP5A  BYTE 36
       TEXT 'Because of its key role in domestic '
       EVEN
EXP5B  BYTE 35
       TEXT 'transportation, Wuhan is sometimes '
       EVEN
EXP5C  BYTE 41
       TEXT 'referred to as "the Chicago of China" by '
       EVEN
EXP5D  BYTE 18
       TEXT 'foreign sources.'
       BYTE 13,10
       EVEN

FMT0   DATA 2,0
* Bold
       DATA 17
       BYTE 3,>84
       DATA 17
       BYTE 5,>04

FMTE2  DATA 2,0
* Bold
       DATA 18
       BYTE 3,>84
       DATA 18
       BYTE 5,>04

FMTE4  DATA 2,0
* Bold
       DATA 16
       BYTE 3,>84
       DATA 16
       BYTE 5,>04

HEXLIN BSS  >20
*
* Do a memory dump
*
* R0 - starting address
* R1 - dump size
MEMDMP MOV  R0,R3
       MOV  R0,R4
       A    R1,R4
*
MEMD0  LI   R1,HEXLIN
MEMD1  MOV  *R3+,R0
       BLWP @MAKETX
       AI   R1,4
       CI   R1,HEXLIN+>20
       JL   MEMD1
*
       MOV  R0,R5
       LI   R0,HEXLIN
       LI   R1,>20
       BLWP @PRINTL
       MOV  R5,R0
*
       C    R3,R4
       JL   MEMD0
       RT
       END