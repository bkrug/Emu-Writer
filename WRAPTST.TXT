       DEF  RUNTST
       DEF  LINLST,FMTLST,MGNLST
*
       REF  WRAP
       REF  BUFINT,BUFALC,BUFREE,BUFCPY
       REF  MAKETX,PRINTL,OPENF,CLOSEF
       REF  VMBW

RUNTST BLWP @OPENF
* Initialize the memory buffer.
       BL   @INTBUF
* Write notification on screen.
       BL   @WRTST
* Loop through every test.
       BL   @TSTLP
* Write notification on screen.
       BL   @WREND
       BLWP @CLOSEF
JMP    LIMI 2
       LIMI 0
       JMP  JMP

* Write notification
WRTST  LI   R0,STARTM
       LI   R1,ENDM-STARTM
       BLWP @PRINTL
       RT
 
* Finished testing
WREND  LI   R0,ENDM
       LI   R1,4
       BLWP @PRINTL
       RT

STARTM TEXT 'Testing'
ENDM   TEXT 'Done'
ENDME  EVEN

*
* Initialize buffer.
*
INTBUF LI   R0,SPACE
       LI   R1,SPCEND-SPACE
       BLWP @BUFINT
       MOV  R0,R0
       JEQ  INTBF2
       LI   R0,INTMSG
       LI   R1,INTMSE-INTMSG
       BLWP @PRINTL
       B    @JMP
INTBF2 RT
INTMSG TEXT 'Failed to initialize memory buffer.'
INTMSE EVEN

****************************************
*
* Main Test Loop.
*
****************************************

TSTLP  MOV  R11,R12
* Use R10 to track which test this is
       CLR  R10
* Loop through each test.
TSTLP1
* Initialize the Line List.
       BL   @INITLL
* Copy each line into the line list.
       BL   @CPYLIN
* Setup wrap parameters.
* Place index of line to wrap in R0.
       LI   R0,WRPLN
       A    R10,R0
       A    R10,R0
       MOV  *R0,R0
* Wrap TEXT
       BLWP @WRAP
* Assign R8 the address of the expected
* line list.
* Assign R9 the address of the actual
* line list.
       LI   R8,EXPLIN
       A    R10,R8
       A    R10,R8
       MOV  *R8,R8
       MOV  @LINLST,R9
* Confirm that the number of lines in
* the list is correct.
       C    *R8,*R9
       JNE  LPERR1
* R5 shall store number of lines.
       MOV  *R8,R5
* Now loop through each line.
* R8 holds address of the address of the
* expected line
* R9 holds address of the address of the
* actual line
TSTLP2 INCT R8
       INCT R9
* Store position within the line
* R6 and R7 hold the position within
* each line.
       MOV  *R8,R6
       MOV  *R9,R7
* R4 shall store character length
       MOVB *R6,R4
       SRL  R4,8
* Confirm that the line character length
* is correct.
       CB   *R6+,*R7+
       JNE  LPERR2
* Increment R6 and R7 by two in order
* to skip the space width check.
       C    *R6+,*R7+
* Confirm string contents are correct.
TSTLP3 CB   *R6+,*R7+
       JNE  LPERR4
       DEC  R4
       JH   TSTLP3
* One test is complete.
* Reinitialize memory buffer.
       LI   R0,SPACE
       LI   R1,SPCEND-SPACE
       BLWP @BUFINT
* See if we should check another line
       DEC  R5
       JL   TSTLP2
* Run the next test
       INC  R10
       CI   R10,4
       JL   TSTLP1
       B    *R12

* Test result contains wrong number of
* lines.
LPERR1 LI   R0,TSTNAM
       MOV  R10,R3
       MPY  @SIX,R3
       A    R4,R0
       LI   R1,LPER1M+5
       LI   R2,6
       BLWP @BUFCPY
*
       MOV  @LINLST,R0
       LI   R1,LPER1N-4
       BLWP @MAKETX
*
       LI   R0,LPER1M
       LI   R1,LPER1N-LPER1M
       BLWP @PRINTL
       B    *R12

* Test line contains wrong number of 
* characters.
LPERR2 LI   R0,TSTNAM
       MOV  R10,R3
       MPY  @SIX,R3
       A    R4,R0
       LI   R1,LPER2M+5
       LI   R2,6
       BLWP @BUFCPY
*
       MOV  R10,R0
       LI   R1,LPER2N
       BLWP @MAKETX
*
       MOV  *R7,R0
       SRL  R0,8
       LI   R1,LPER2N+11
       BLWP @MAKETX
*
       LI   R0,LPER2M
       LI   R1,LPER2O-LPER2M
       BLWP @PRINTL
*
       B    *R12

* Test line contents don't match
LPERR4 
* Copy test name.
       LI   R0,TSTNAM
       MOV  R10,R3
       MPY  @SIX,R3
       A    R4,R0
       LI   R1,LPER4M+5
       LI   R2,6
       BLWP @BUFCPY
* Record line index
       MOV  R9,R0
       S    @LINLST,R0
       SRL  R0,1
       DEC  R0
       LI   R1,LPER4N
       BLWP @MAKETX
* Copy expected text
       LI   R1,LPER4O
       MOV  *R8,R0
       MOVB *R0,R2
       AI   R0,3
       SRL  R2,8
       CI   R2,60
       JL   LPER4A
       LI   R2,60
LPER4A BLWP @BUFCPY
* Copy "|-- Actual:" into message
       LI   R0,LPER4Q
       A    R2,R1
       LI   R2,LPER4R-LPER4Q
       BLWP @BUFCPY
* Copy actual text
       A    R2,R1
       MOV  *R9,R0
       MOVB *R0,R2
       AI   R0,3
       SRL  R2,8
       CI   R2,60
       JL   LPER4B
       LI   R2,60
LPER4B BLWP @BUFCPY
* Copy "|--" into message.
       LI   R0,LPER4Q
       A    R2,R1
       LI   R2,3
       BLWP @BUFCPY
* Output error message.
       LI   R0,LPER4M
       LI   R1,LPER4Q-LPER4M
       BLWP @PRINTL
*
       B    *R12

LPER1M TEXT 'Test ...... failed. Wrong '
       TEXT 'number of lines. We see '
       TEXT '....'
LPER1N EVEN

LPER2M TEXT 'Test ...... failed. Wrong '
       TEXT 'number of characters in '
       TEXT 'line '
LPER2N TEXT '....'
       TEXT ' found .... characters'
LPER2O EVEN

LPER4M TEXT 'Test ...... failed. '
       TEXT 'Contents of line '
LPER4N TEXT '.... were wrong. Expected:'
LPER4O TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '                    '
       TEXT '           '
LPER4P 
LPER4Q TEXT '|-- Actual:'
LPER4R EVEN

*
* Initialize Line List for the
* current test.
* Reserve space for the list and store
* length of the list in first word.
*
* Input:
*   R10 - Index of current test
* Output:
*   R0 - address of the line list within
*        buffer
*   R8 - address of the source line list
*        (like LIN0)
INITLL
* Get address of starting line list
       LI   R8,STRLIN
       A    R10,R8
       A    R10,R8
       MOV  *R8,R8
* R8 now contains the address of 
* initial line list.
* Reserve space for line list in buffer.
       MOV  *R8,R0
       INC  R0
       SLA  R0,1
*       LI   R0,>20
       BLWP @BUFALC
* Copy starting line list to LINLST.
* R0 holds the location that LINLST is
* stored at.
       MOV  R0,@LINLST
* Copy number of lines to Line List.
       MOV  *R8,*R0
*
       RT

*
* Copy strings into buffer.
* Each string starts with the number of
* characters stored in the 1st byte.
* The memory consumed is equal to that 
* plus three.
*
* Also, store the address of copied
* strings in the line list.
*
* Input:
*   R0 - Address of Line List
*   R8 - Address of Source Line List
*        (Like LINL0)
* Output:
*   R8,R9,R1,R0,R2,R3
CPYLIN
* Store Address of new Line List in R9.
       MOV  R0,R9
* Store number of lines to copy in R3.
       MOV  *R8,R3
CPYL2 
* R8 and R9 hold address of the address
* the lines of text
       INCT R8
       INCT R9
* R0 will contain address of line to
* copy.
       MOV  *R8,R0
* Change R0's value to number of bytes
* to copy and allocate that much space.
       MOVB *R0,R0
       SRL  R0,8
       AI   R0,3
       MOV  R0,R2
       BLWP @BUFALC
* Copy the string. R2 still holds byte
* size to copy.
       MOV  R0,R1
       MOV  *R8,R0
       BLWP @BUFCPY
* Put the new string address in the
* line list.
       MOV  R1,*R9
* If there are more lines, loop.
       DEC  R3
       JH   CPYL2
*
       RT

SPACE  BSS  >2000
SPCEND
* Address of LINLST
LINLST DATA 0

****************************************
*
* Test data
*
****************************************

*Lines to wrap from
WRPLN  DATA 0,0,0,0
*Original Line Lists
STRLIN DATA LINL0,LINL0,LINL0,LINL0
STRLNE
*Expected Line Lists
EXPLIN DATA LINE1,LINE2,LINE3,LINE4
EXPLNE
*Test names
TSTNAM TEXT 'LIN1  LIN2  LIN3  LIN4  '
TSTNME
SIX    DATA 6

LINL0  DATA 15
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
LINL0E

* Expected Line results
LINE1  DATA 15
       DATA EXP1A,EXP1B,EXP1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
LINE1E
* Expected Line results
LINE2  DATA 16
       DATA LIN1A,LIN1B,LIN1C
       DATA EXP2A,EXP2B,EXP2C,EXP2D
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
LINE2E
* Expected Line results
LINE3  DATA 15
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA EXP3A,EXP3B,EXP3C,EXP3D
       DATA LIN4A,LIN4B,LIN4C,LIN4D,LIN4E
LINE3E
* Expected Line results
LINE4  DATA 14
       DATA LIN1A,LIN1B,LIN1C
       DATA LIN2A,LIN2B,LIN2C
       DATA LIN3A,LIN3B,LIN3C,LIN3D
       DATA EXP4A,EXP4B,EXP4C,EXP4D
LINE4E

*
* Data Format:
* Byte 0: Number of characters in the
*         string.
* Byte 1&2: Length of text in format:
*           n/120 inches.
* Byte >=3: The string
*
* At 10 CPI bytes 1&2 will normally be
* equal to either (byte0 - 1)*12 or
* (byte0 - 2)*12. Each character is
* 12/120 inch wide. And spaces or line
* endings are not included in text
* length.

*
* The entire paragraph will be
* re-wrapped. A word will be brought
* down to the next line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN1A  BYTE 47,>02,>28
*       DATA 552
       TEXT 'Wuhan is the capital of Hubei province, '
       TEXT 'China, '
LIN1B  BYTE 41,>01,>E0
*       DATA 480
       TEXT 'and is the most populous city in Central '
LIN1C  BYTE 8,>0,>0
*       DATA 72
       TEXT 'China.'
       BYTE 13,10
EXP1A  BYTE 40,>0,>0
*       DATA 468
       TEXT 'Wuhan is the capital of Hubei province, '
EXP1B  BYTE 40,>0,>0
*       DATA 468
       TEXT 'China, and is the most populous city in '
EXP1C  BYTE 16,>0,>0
*       DATA 168
       TEXT 'Central China.'
       BYTE 13,10
       EVEN

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* down to the next line, and a new line
* will be created.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN2A  BYTE 60,>0,>0
*       DATA 708
       TEXT 'It lies in the eastern Jianghan Plain '
       TEXT 'on the middle reaches '
LIN2B  BYTE 41,>0,>0
*       DATA 480
       TEXT 'of the Yangtze River at the intersection '
LIN2C  BYTE 42,>0,>0
*       DATA 504
       TEXT 'of the Yangtze and Han rivers.'
       BYTE 13,10
EXP2A  BYTE 38,>0,>0
*       DATA 444
       TEXT 'It lies in the eastern Jianghan Plain '
EXP2B  BYTE 37,>0,>0
*       DATA 432
       TEXT 'on the middle reaches of the Yangtze '
EXP2C  BYTE 33,>0,>0
*       DATA 384
       TEXT 'River at the intersection of the '
EXP2D  BYTE 25,>0,>0
*       DATA 300
       TEXT 'Yangtze and Han rivers.'
       BYTE 13,10

*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* up to the first line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN3A  BYTE 19,>0,>0
*       DATA 216
       TEXT 'Arising out of the '
LIN3B  BYTE 32,>0,>0
*       DATA 372
       TEXT 'conglomeration of three cities, '
LIN3C  BYTE 39,>0,>0
*       DATA 456
       TEXT 'Wuchang, Hankou, and Hanyang, Wuhan is '
LIN3D  BYTE 34,>0,>0
*       DATA 384
       TEXT 'known as "China"s Thoroughfare".'
       BYTE 13,10
EXP3A  BYTE 37,>0,>0
*       DATA 432
       TEXT 'Arising out of the conglomeration of '
EXP3B  BYTE 35,>0,>0
*       DATA 408
       TEXT 'three cities, Wuchang, Hankou, and '
EXP3C  BYTE 36,>0,>0
*       DATA 420
       TEXT 'Hanyang, Wuhan is known as "China"s '
EXP3D  BYTE 16,>0,>0
*       DATA 168
       TEXT 'Thoroughfare".'
       BYTE 13,10
       
*
* The entire paragraph will be
* re-wrapped. Some words will be brought
* up to the first line. The final line
* of the paragraph will be removed.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
LIN4A  BYTE 29,>0,>0
*       DATA 336
       TEXT 'It is a major transportation '
LIN4B  BYTE 36,>0,>0
*       DATA 420
       TEXT 'hub, with dozens of railways, roads '
LIN4C  BYTE 36,>0,>0
*       DATA 420
       TEXT 'and expressways passing through the '
LIN4D  BYTE 35,>0,>0
*       DATA 408
       TEXT 'city and connecting to other major '
LIN4E  BYTE 9,>0,>0
*       DATA 84
       TEXT 'cities.'
       BYTE 13,10
EXP4A  BYTE 39,>0,>0
*       DATA 456
       TEXT 'It is a major transportation hub, with '
EXP4B  BYTE 39,>0,>0
*       DATA 468
       TEXT 'dozens of railways, roads and '
EXP4C  BYTE 37,>0,>0
*       DATA 432
       TEXT 'expressways passing through the city '
EXP4D  BYTE 39,>0,>0
*       DATA 444
       TEXT 'and connecting to other major cities.'
       BYTE 13,10

HEXLIN BSS  >20
*
* Do a memory dump
*
* R0 - starting address
MEMDMP MOV  R0,R3
       MOV  R0,R4
       AI   R4,>140
*
MEMD0  LI   R1,HEXLIN
MEMD1  MOV  *R3+,R0
       BLWP @MAKETX
       AI   R1,4
       CI   R1,HEXLIN+>20
       JL   MEMD1
*
       MOV  R0,R5
       LI   R0,HEXLIN
       LI   R1,>20
       BLWP @PRINTL
       MOV  R5,R0
*
       C    R3,R4
       JL   MEMD0
       B    *R12
       END