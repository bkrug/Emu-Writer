       DEF  TSTLST,RSLTFL
       REF  AEQ,ANEQ,ABLCK
*
       REF  WRAP,LINLST,FMTLST,MGNLST
       REF  BUFINT,BUFALC,BUFCPY
	   

TSTLST DATA TSTEND-TSTLST-2/8
* Calculate relative document-line
* looking backwards within the same
* paragraph
       DATA WRP1
       TEXT 'WRP1  '
TSTEND
RSLTFL BYTE RSLTFE-RSLTFL-1
       TEXT 'DSK2.TESTRESULT.TXT'
RSLTFE
       EVEN

* PARAGRAPH FORMAT
*
* Data Format:
* Byte 0&1: Length of string (paragraph)
* Byte 2&3: Address of Wrap List
* Byte >=4: The string
*
* At 10 CPI bytes 1&2 will normally be
* equal to either (byte0 - 1)*12 or
* (byte0 - 2)*12. Each character is
* 12/120 inch wide. And spaces or line
* endings are not included in text
* length.
*

MNERR  TEXT 'No error should have been '
       TEXT 'detected.'
MNERRE
MWRP   TEXT 'Wrap lists do not match '
MWRPE  EVEN
FAKEAD DATA >FFFE

*
* The entire paragraph will be
* re-wrapped. A word will be brought
* down to the next line.
*
* Assume a page width of eight inches
* with two inch margins, no indent,
* 10 CPI
*
WRP1
* Arrange
       LI   R12,STACK
	   MOV  R11,*R12+
* The wrap list must go in the block
* of memory allocated to the chunck
* manager because it may be deallocated.
       LI   R2,WRPL1
	   LI   R3,WRPE1-WRPL1
	   LI   R4,PAR1A
       BL   @CPYWRP
*
       LI   R0,PARL1
	   MOV  R0,@LINLST
	   LI   R0,FMT0
	   MOV  R0,@FMTLST
	   LI   R0,MRGN0
	   MOV  R0,@MGNLST
* Act
       LI   R0,3
	   BLWP @WRAP
* Assert
*  No error should be detected
       MOV  R0,R1
	   SETO R0
	   LI   R2,MNERR
	   LI   R3,MNERRE-MNERR
	   BLWP @ANEQ
*  Wrap list should match expected
       LI   R0,WRPE1
	   MOV  @PAR1A+2,R1
	   LI   R2,WRPE1E-WRPE1
	   LI   R3,MWRP
	   LI   R4,MWRPE-MWRP
	   BLWP @ABLCK
*
       DECT R12
	   MOV  *R12,R11
       RT


* Paragraph List
PARL1  DATA 5,1
       DATA FAKEAD
	   DATA FAKEAD
	   DATA FAKEAD
       DATA PAR1A
       DATA FAKEAD

PAR1A  DATA 47+41+6
       DATA WRPL1
       TEXT 'Wuhan is the capital of Hubei province, '
       TEXT 'China, and is the most populous city in '
       TEXT 'Central China.'
       EVEN
WRPL1  DATA 2,1,47,47+41
WRPE1  DATA 2,1,40,40+40
WRPE1E


FMT0   DATA 3,3
* 12-Cpi at paragraph start
       DATA 4,0,0,10
* 5-Cpi in middle of second line of 
* paragraph
       DATA 5,64,0,24
* 12-Cpi at paragraph start
       DATA 7,0,0,10
FMT0E

MRGN0  DATA 4,3
* 2-inch margins
       DATA 0,>0000,>2828,>1414
* 1-inch margins
       DATA 7,>0000,>1414,>1414
* 2-inch margins, 1-inch indent
       DATA 9,>0014,>2828,>1414
* 2.5-inch margins,
* 0.5-inch hanging indent
       DATA 10,>00F6,>3232,>1414
MRGN0E

*
* Copy Wrap Address to buffer.
* Register its location in
* paragraph object.
* Input:
*  R2 - Wrap list source address
*  R3 - length of wrap list
*  R4 - address of paragraph
* Output:
*  R0-R2,R4 will change
CPYWRP
       LI   R0,SPACE
       LI   R1,SPCEND-SPACE
       BLWP @BUFINT
*
       MOV  R3,R0
	   BLWP @BUFALC
*
       MOV  R0,R1
	   MOV  R2,R0
	   MOV  R3,R2
	   BLWP @BUFCPY
* Update paragraph object's wrap address
       INCT R4
	   MOV  R1,*R4
*
	   RT

SPACE  BSS  >400
SPCEND
STACK  BSS  >80
       END